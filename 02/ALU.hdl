// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // ZX, ZY
    // zero x & y inputs if necessary

    // Fewer gates version of conditional zeroing
    Not16(in[0]=zx, in[1]=zx, in[2]=zx, in[3]=zx, in[4]=zx, in[5]=zx, in[6]=zx, in[7]=zx, in[8]=zx, in[9]=zx, in[10]=zx, in[11]=zx, in[12]=zx, in[13]=zx, in[14]=zx, in[15]=zx, out=nzx);
    Not16(in[0]=zy, in[1]=zy, in[2]=zy, in[3]=zy, in[4]=zy, in[5]=zy, in[6]=zy, in[7]=zy, in[8]=zy, in[9]=zy, in[10]=zy, in[11]=zy, in[12]=zy, in[13]=zy, in[14]=zy, in[15]=zy, out=nzy);
    And16(a=x, b=nzx, out=zeroedX);
    And16(a=y, b=nzy, out=zeroedY);

    // NX, NY
    // negate x & y inputs if necessary
    Not16(in=zeroedX, out=tNottedX);
    Not16(in=zeroedY, out=tNottedY);
    Mux16(a=zeroedX, b=tNottedX, sel=nx, out=negatedX);
    Mux16(a=zeroedY, b=tNottedY, sel=ny, out=negatedY);

    // F
    // perform either x + y or x & y depending on f
    And16(a=negatedX, b=negatedY, out=xAndY);
    Add16(a=negatedX, b=negatedY, out=xPlusY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=funcOut);

    // NO
    // negate output if no is set to 1
    Not16(in=funcOut, out=negatedFuncOut);
    Mux16(a=funcOut, b=negatedFuncOut, sel=no, out[0..7]=lowerOut, out[8..14]=higherOut, out[15]=ngBit);
    
    // ZR 
    // out == 0 ? 1 : 0
    Or8Way(in=lowerOut, out=orOut1);
    Or8Way(in[0..6]=higherOut, in[7]=ngBit, out=orOut2);
    Or(a=orOut1, b=orOut2, out=tzr);
    Not(in=tzr,out=zr);
    
    // NG 
    // out < 0 ? 1 : 0
    And(a=ngBit,b=true,out=ng);

    // OUTPUT 
    Or16(a[0..7]=lowerOut,a[8..14]=higherOut,a[15]=ngBit,out=out);
}